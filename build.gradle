buildscript {
    repositories {
        maven {
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.0.+') {
            changing = true
        }
        classpath 'org.spongepowered:mixingradle:0.6-FG1.2'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'

ext { // forgegradle reads project properties during it's application
    runArgs = '--tweakClass org.spongepowered.asm.launch.MixinTweaker'
}

apply plugin: 'forge'
apply plugin: 'org.spongepowered.mixin'

ext {
    mcversion = '1.7.10'
}

version = '1.0.0'
group = 'ru.qwert21'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

sourceSets {
    main {
        output.resourcesDir = "build/classes/java/main"
    }
}

configurations {
    compile.extendsFrom shadow
}

minecraft {
    version '1.7.10-10.13.4.1614-1.7.10'
    mappings 'stable_8'

    runDir 'run'
}

mixin {
    add sourceSets.main, 'mixins.hidemod.refmap.json'
}

repositories {
    maven {
        url 'https://repo.spongepowered.org/maven/'
    }
}

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    shadow('org.spongepowered:mixin:0.7.10-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }
}

jar {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    appendix project.mcversion

    manifest {
        attributes([
            "Implementation-Title": project.name,
            "Implementation-Version": project.mcversion + '-' + project.version,
            "Implementation-Vendor": "qwert21",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),

            "MixinConfigs": "mixins.hidemod.json",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "ForceLoadAsMod": true,
            "FMLCorePluginContainsFMLMod": true
        ])
    }

    from {
        configurations.shadow.collect {
            zipTree(it).matching {
                exclude '**/*.RSA', '**/*.SF'
            }
        }
    }

    finalizedBy 'reobf'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.mcversion

    from(sourceSets.main.resources) {
        include 'mcmod.info'

        expand([
            'version': project.version,
            'mcversion': project.mcversion
        ])
    }
}