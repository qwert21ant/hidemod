buildscript {
    repositories {
        maven {
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

version = '1.0'
group = 'ru.qwert21.hidemod'
archivesBaseName = 'hidemod'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

sourceSets {
    main {
        output.resourcesDir = "build/classes/java/main"
    }
}

configurations {
    compile.extendsFrom shadow
}

minecraft {
    version "1.8.9-11.15.1.2318-1.8.9"
    mappings "stable_20"

    runDir "run"

    clientRunArgs += ['--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker']

    makeObfSourceJar = false
}

mixin {
    add sourceSets.main, 'mixins.hidemod.refmap.json'
}

repositories {
    maven {
        url 'https://repo.spongepowered.org/maven/'
    }
}

dependencies {
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    shadow('org.spongepowered:mixin:0.7.10-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'gson'
        exclude module: 'guava'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes([
            "Implementation-Title": project.name,
            "Implementation-Version": version,
            "Implementation-Vendor": "qwert21",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),

            "MixinConfigs": "mixins.hidemod.json",
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "ForceLoadAsMod": true
        ])
    }

    from {
        configurations.shadow.collect {
            zipTree(it).matching {
                exclude '**/*.RSA', '**/*.SF'
            }
        }
    }

    finalizedBy 'reobfJar'
}